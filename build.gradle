plugins {
    id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.sonatype.gradle.plugins.scan' version '3.0.0'
}

group 'io.github.kamilszewc'
version '6.1'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'

    implementation 'commons-codec:commons-codec:1.18.0'

    testImplementation 'org.apache.groovy:groovy:4.0.26'
    testImplementation 'org.spockframework:spock-core:2.4-M5-groovy-4.0'
}

test {
    useJUnitPlatform()
}


java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                if (project.properties.containsKey('mavenCentralUser')) {
                    username = "$mavenCentralUser"
                }
                if (project.properties.containsKey('mavenCentralPassword')) {
                    password = "$mavenCentralPassword"
                }
            }
        }
    }
    publications {

        mavenJava(MavenPublication) {
            artifactId = 'totp'
            from components.java
            pom {
                name = 'Totp'
                description = 'Time-based One Time Password (TOTP) generator'
                url = 'https://github.com/kamilszewc/totp'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kszewc'
                        name = "Kamil Szewc"
                        email = 'kamil.szewc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kamilszewc/totp.git'
                    developerConnection = 'scm:git:https://github.com/kamilszewc/totp.git'
                    url = 'https://github.com/kamilszewc/totp.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
    sign configurations.archives
//    sign publishing.publications.relocation
}

ossIndexAudit {
    allConfigurations = false
    useCache = false
    outputFormat = 'DEFAULT'
}